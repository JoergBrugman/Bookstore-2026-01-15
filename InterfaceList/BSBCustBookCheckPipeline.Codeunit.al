codeunit 50110 "BSB Cust. Book Check Pipeline"
{
    procedure ProcessPipeline(Customer: Record Customer)
    var
        Step: Interface "BSB Cust. Book Check Step";
        Steps: List of [Interface "BSB Cust. Book Check Step"];
    begin
        CollectSteps(Steps);
        SortSteps(Steps);

        foreach Step in Steps do
            if Step.IsEnabled(Customer) then
                Message(Step.Execute(Customer));
    end;

    local procedure CollectSteps(var Steps: List of [Interface "BSB Cust. Book Check Step"])
    begin
        OnRegisterCustBookCheckSteps(Steps);
    end;

    local procedure SortSteps(var Steps: List of [Interface "BSB Cust. Book Check Step"])
    var
        Integers: Record Integer;
        Sorted: List of [Interface "BSB Cust. Book Check Step"];
        Step: Interface "BSB Cust. Book Check Step";
    begin
        // Sequences einsammeln
        foreach Step in Steps do begin
            Integers.Get(Step.GetSequence());
            Integers.Mark(true);
        end;
        // Auf markierte Datensätze filtern 
        Integers.MarkedOnly(true);

        // Die Sequences nacheinander durchgehen
        if Integers.FindSet() then
            repeat
                // Alle Steps nacheinander durchgehen  
                foreach Step in Steps do
                    // Jetzt die Interfaces, die einer Sequence angehören, nacheinander übertragen
                    if Step.GetSequence() = Integers.Number then
                        Sorted.Add(Step);
            until Integers.Next() = 0;
        Steps := Sorted;
    end;

    [IntegrationEvent(false, false)]
    local procedure OnRegisterCustBookCheckSteps(var Steps: List of [Interface "BSB Cust. Book Check Step"])
    begin
    end;

}